#
# Copyright Â© 2025 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/ten_runtime/feature/test.gni")
import("//build/ten_runtime/ten.gni")

ten_package_test_prepare_auxiliary_resources(
    "standalone_test_nodejs_2_test_files") {
  resources = [
    "__init__.py",
    "test_case.py",
  ]

  utils_files = exec_script("//.gnfiles/build/scripts/glob_file.py",
                            [
                              "--dir",
                              rebase_path("//tests/utils/**/*"),
                              "--dir-base",
                              rebase_path("//tests/utils"),
                              "--recursive",
                              "--only-output-file",
                            ],
                            "json")

  foreach(utils_file, utils_files) {
    utils_file_rel_path = utils_file.relative_path
    resources +=
        [ "//tests/utils/${utils_file_rel_path}=>utils/${utils_file_rel_path}" ]
  }

  all_ext_files = exec_script(
          "//.gnfiles/build/scripts/glob_file.py",
          [
            "--dir",
            rebase_path(
                "//tests/ten_runtime/integration/nodejs/standalone_test_nodejs_2/default_extension_nodejs/**/*"),
            "--dir-base",
            rebase_path(
                "//tests/ten_runtime/integration/nodejs/standalone_test_nodejs_2/default_extension_nodejs"),
            "--recursive",
            "--only-output-file",
          ],
          "json")

  foreach(ext_file, all_ext_files) {
    ext_file_rel_path = ext_file.relative_path
    resources += [ "//tests/ten_runtime/integration/nodejs/standalone_test_nodejs_2/default_extension_nodejs/${ext_file_rel_path}=>default_extension_nodejs/${ext_file_rel_path}" ]
  }
}

group("standalone_test_nodejs_2") {
  deps = [
    ":standalone_test_nodejs_2_test_files",
    "//packages/core_extensions/default_extension_nodejs:upload_default_extension_nodejs_to_server",
  ]
}
